grammar org.chw.game.ide.Proto hidden(WS)//with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate proto "http://www.chw.org/game/ide/Proto"

//包定义
Model :((PACKAGE name=QualifiedName END?) | imports+=ImportInfo | options+=OptionInfo | elements+=Element | comm+=Comment_ALL)*;

//导入信息
ImportInfo: IMPORT importURI=SL_STRING END?;

//参数信息
OptionInfo:	OPTION name=ENTITY_NAME EQUALS value=SL_STRING END? ;

//元素定义
Element:Type|Map;

//通用定义
Type : MessageType | EnumType;

//映射
Map	 : MAP_KIND name=ENTITY_NAME FILETER_A bind=[MessageType|QualifiedName] BRACE_L (comm+=Comment_ALL | fields+=MapField)* BRACE_R END?;
MapField:attr=FIELD_ATTR name=ENTITY_NAME (FILETER_B filter=[Map|QualifiedName])? FILETER_A links=MapRef END?;
MapRef:link=[MessageField|IDENTITY] (DOT child=MapRef)?;

//消息定义
MessageType  :  MESSAGE name=ENTITY_NAME BRACE_L (comm+=Comment_ALL | fields+=MessageField | types+=Type)* BRACE_R END? ;
MessageField :	MessageSimpleField | MessageComplexField;
MessageSimpleField  : attr=FIELD_ATTR type=FIELD_TYPE           name=ENTITY_NAME EQUALS index=NUMBER MessageFieldDefault? END? ;
MessageComplexField : attr=FIELD_ATTR type=[Type|QualifiedName] name=ENTITY_NAME EQUALS index=NUMBER MessageFieldDefault? END? ;
MessageFieldDefault : BRACKET_L DEFAULT_KIND EQUALS (NUMBER|IDENTITY) BRACKET_R ;

//枚举定义
EnumType  : ENUM name=ENTITY_NAME BRACE_L (comm+=Comment_ALL | fields+=EnumField | options+=EnumFieldOpt)* BRACE_R END? ;
EnumField : name=ENTITY_NAME EQUALS index=NUMBER END? ;
EnumFieldOpt : OPTION name=ENTITY_NAME EQUALS value=IDENTITY END?;

//公用
QualifiedName : IDENTITY (DOT IDENTITY)*;
ENTITY_NAME : FIELD_ATTR | FIELD_TYPE | IDENTITY | PACKAGE | IMPORT | OPTION | MESSAGE | ENUM | MAP_KIND | DEFAULT_KIND;
Comment_ALL:text=COMMENT_SL|text=COMMENT_ML;

terminal BRACE_L:'{';
terminal BRACE_R:'}';
terminal BRACKET_L:'[';
terminal BRACKET_R:']';
terminal EQUALS:'=';
terminal END:';';
terminal DOT:'.';
terminal FILETER_A:'<=';
terminal FILETER_B:'<-';
terminal NUMBER returns ecore::EInt: '-'? ('0'..'9' | '1'..'9' '0'..'9'+);

terminal PACKAGE	: 'package';
terminal IMPORT	: 'import';
terminal OPTION  	: 'option';
terminal MESSAGE 	: 'message';
terminal ENUM	 	: 'enum';
terminal MAP_KIND  : 'map';
terminal DEFAULT_KIND : 'default';
terminal FIELD_ATTR	: 'optional' | 'repeated' | 'required';
terminal FIELD_TYPE	:'int32' | 'sint32' | 'uint32' | 'int64' | 'sint64' | 'uint64' | 'float' | 'double' | 'string' | 'bool' | 'bytes';
terminal IDENTITY		: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'0'..'9'|'_')*;

terminal COMMENT_ML	: '/*' -> '*/';
terminal COMMENT_SL	: '//' !('\n'|'\r')*;
terminal SL_STRING		: '"' !('\n'|'\r'|'"')* '"';
terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER	: .;

