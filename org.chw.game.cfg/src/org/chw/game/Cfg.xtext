grammar org.chw.game.Cfg hidden(WS) //with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cfg "http://www.chw.org/game/Cfg"

XML2:
	pack=PackDef?
	(types+=Type | otherComm+=COMMENT)*;

PackDef:
	packCHAR=C_PACKAGE pack=PackName C_SEMICOLON?;

Type:
	comm=COMMENT input=Input? prefix=C_TYPE name=NAME C_BRACE_L (fields+=Field | otherComm+=COMMENT)* C_BRACE_R;

Input:
	C_BRACKET_L prefix=C_INPUT C_PAREN_L C_FILE C_EQUALS filePath=STRING (C_COMMA C_NODE C_EQUALS nodePath=STRING)?
	C_PAREN_R
	C_BRACKET_R;

Field:
	comm=COMMENT type=(NativeType | ListType | HashType) fieldName=NAME C_EQUALS nodePath=STRING C_SEMICOLON?;

NativeType:
	type=TypeName;

ListType:
	C_LIST C_ANGLE_L type=TypeName C_ANGLE_R;

HashType:
	C_HASH C_ANGLE_L type=TypeName C_ANGLE_R (C_PAREN_L params+=Param (C_COMMA params+=Param)* C_PAREN_R)?;

Param:
	paramName=NAME;

PackName:
	NAME ('.' NAME)*;

TypeName:
	C_INPUT | C_FILE | C_NODE | C_TYPE | C_INT | C_UINT | C_BOOL | C_NUMBER | C_STRING | C_LIST | C_HASH | NAME;

terminal C_BRACKET_L:
	'[';

terminal C_BRACKET_R:
	']';

terminal C_PAREN_L:
	'(';

terminal C_PAREN_R:
	')';

terminal C_BRACE_L:
	'{';

terminal C_BRACE_R:
	'}';

terminal C_ANGLE_L:
	'<';

terminal C_ANGLE_R:
	'>';

terminal C_EQUALS:
	'=';

terminal C_COMMA:
	',';

terminal C_SEMICOLON:
	';';

terminal C_INPUT:
	'input';

terminal C_FILE:
	'file';

terminal C_NODE:
	'node';

terminal C_TYPE:
	'type';

terminal C_INT:
	'int';

terminal C_UINT:
	'uint';

terminal C_BOOL:
	'Boolean';

terminal C_NUMBER:
	'Number';

terminal C_STRING:
	'String';

terminal C_LIST:
	'List';

terminal C_HASH:
	'Hash';

terminal C_PACKAGE:
	'package';

terminal NAME:
	('a'..'z' | 'A'..'Z') ('a'..'z' | 'A'..'Z' | '0'..'9' | '_')*;

terminal STRING:
	'"' !('\r' | '\n' | '"')* '"';

terminal fragment COMMENT_END:
	'*/';

terminal COMMENT:
	'/*'->COMMENT_END | '//' !('\r' | '\n')*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;
	